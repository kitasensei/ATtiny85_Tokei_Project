#include <Arduino.h>

#define CLK 5
#define DIO 6

// 1バイトを送信する関数
bool writeByte(uint8_t data) {
    for (uint8_t i = 0; i < 8; i++) {
        digitalWrite(CLK, LOW);
        delayMicroseconds(10);
        
        if ((data >> i) & 0x01) {
            // ビットが1の場合
            pinMode(DIO, INPUT);
        } else {
            // ビットが0の場合
            pinMode(DIO, OUTPUT);
            digitalWrite(DIO, LOW);
        }
        
        delayMicroseconds(10);
        digitalWrite(CLK, HIGH);
        delayMicroseconds(10);
    }
    
    // ACKの確認
    digitalWrite(CLK, LOW);
    delayMicroseconds(10);
    pinMode(DIO, INPUT); // DIOピンの制御を解放
    delayMicroseconds(10);
    bool ack = (digitalRead(DIO) == LOW);

    digitalWrite(CLK, HIGH);
    delayMicroseconds(10);
    if (!ack) {
        // ACKが失敗した場合、DIOを再び制御
        pinMode(DIO, OUTPUT);
        digitalWrite(DIO, LOW);
    }

    return ack;
}

// START信号
void start() {
    digitalWrite(CLK, HIGH);
    digitalWrite(DIO, HIGH);
    delayMicroseconds(10);
    digitalWrite(DIO, LOW);
    delayMicroseconds(10);
    digitalWrite(CLK, LOW);
    delayMicroseconds(10);
}

// STOP信号
void stop() {
    digitalWrite(CLK, LOW);
    digitalWrite(DIO, LOW);
    delayMicroseconds(10);
    digitalWrite(CLK, HIGH);
    delayMicroseconds(10);
    digitalWrite(DIO, HIGH);
    delayMicroseconds(10);
}

void setup() {
    Serial.begin(9600);
    pinMode(CLK, OUTPUT);
    pinMode(DIO, OUTPUT);
    digitalWrite(CLK, HIGH);
    digitalWrite(DIO, HIGH);

    // TM1637の初期設定
    start();
    writeByte(0x8F); // ディスプレイON, 明るさ最大
    stop();

    Serial.println("Arduino ready.");
}

void loop() {
  // セッション1: データコマンドを送信して終了
start();
writeByte(0x40);
stop();

// セッション2: アドレスとデータを送信して終了
start();
writeByte(0xC0);

  writeByte(0b00000110); // 1
  writeByte(0b01011011); // 2
  writeByte(0b01001111); // 3
  writeByte(0b01100110); // 4

  stop();

  // セッション2: 表示設定
  start();
  writeByte(0x8F); 
  stop();

  delay(2000);
}
